---
title: "Fitting a Dynamic Measurement Burst Model in Mplus"
format: html
editor: visual
#bibliography: reference.bib
---
On this page, we present the Mplus model syntax used to specify the DMBM, as introduced in Andresen et. al. (in prep). This syntax corresponds with models 2a and 2b, as presented in section 3.3.2 of the paper.

# Specifying the DMBM in four Steps

We can specify the DMBM in four steps.

1.  Specify the *within* part, where we specify the micro-level model for each burst separately. Per burst, we specify autoregressive parameters `phi1,  phi2, phi3,...; phiBurst`. Optionally, we can additionally specify the random residual variances (`rv1,rv2,rv3...;`) per burst, which can then be further modeled as the log residual variance (i.e., the compound of reactivity and exposure to shocks) at the `%BETWEEN%` level.


2.  Specify the *between* part, where the random intercepts are specified. It is specified using the `BY` command, `RI_Y BY Y1@1 Y2@1 Y#@1...;`, where `@1` fixes the factor loading to one. This process is repeated for every process feature modeled at the meso- and macro-level. Furthermore, for each feature at each burst, we define the burst- and person-specific deviations from the person's random intercept (i.e., the within-person components *WC*). For example, for the mean at burst 1, we write: `WCY1 BY Y1; Y1@0.001;` which defines the within-person component for the mean and fixes the prior for residual variance of the observed mean `Y1` to (almost) zero.


3.  Specify the *meso-level dynamics* between burst- and person-specific deviations from the person's random intercept of that feature at each burst, using `WCY2 WCY3 ON WCY1 WCY2; ...`, to define autoregressive effects, and `WCY2 WCY3 ON WCPHI1 WCPHI2; ...` to define cross-lagged effects.


4.  Specify relevant *covariances* and residual covariances at the meso-level. Here, we constrain residual covariances to be equal at all bursts (e.g., `WCY2 WITH WCRV2 (rc1); WCY3 WITH WCRV3 (rc1);`) to simplify the model.

Below, we present syntax for each of these steps, for models 2a (excl. random residual variances) and 2b (incl. random residual variances) presented in the paper. Please note that Mplus defaults vary in some places between the two models.

# Step 1: Specifying the `WITHIN` Level

::: panel-tabset
## %WITHIN% Model 2a

The syntax for specifying the `WITHIN` part of the DMBM excluding random residual variances (i.e., model 2a) is given below.

``` default
TITLE:    Mode2a DMBM;
DATA:     FILE = "Example.dat";

VARIABLE: NAMES = ID hour Y1 Y2 Y3;
          MISSING=.;

          CLUSTER = ID;
          USEVAR = Y1 Y2 Y3 ;

          LAGGED = Y1(1) Y2(1) Y3(1);

          TINTERVAL = hour(1);
          MISSING = ALL(999);

ANALYSIS:
          TYPE = TWOLEVEL RANDOM;
          ESTIMATOR = BAYES;
          FBITERATIONS = (50000);
          THIN = 10;
          CHAINS=2;
          PROCESSORS = 40;

MODEL:

%WITHIN%
   	 
! PER BURST: SPECIFY AR(1) PROCESS WITH A RANDOM AUTOREGRESSION

          phi1 | Y1 ON Y1&1;
          phi2 | Y2 ON Y2&1;
          phi3 | Y3 ON Y3&1;

! SET COVARIANCES BETWEEN BURST-SPECIFIC OBSERVATIONS TO ZERO (NOT DEFAULT)
  	      Y1 Y2 WITH Y3@0;
          Y1 WITH Y2@0;
```

## %WITHIN% Model 2b

The syntax for specifying the `WITHIN` part of the DMBM including random residual variances (i.e., model 2b) is given below.

``` default
TITLE:    Mode2b DMBM;
DATA:     FILE = "Example.dat";

VARIABLE: NAMES = ID hour Y1 Y2 Y3;
          MISSING=.;

          CLUSTER = ID;
          USEVAR = Y1 Y2 Y3 ;

          LAGGED = Y1(1) Y2(1) Y3(1);

          TINTERVAL = hour(1);
          MISSING = ALL(999);

ANALYSIS:
          TYPE = TWOLEVEL RANDOM;
          ESTIMATOR = BAYES;
          FBITERATIONS = (50000);
          THIN = 10;
          CHAINS=2;
          PROCESSORS = 40;

MODEL:

%WITHIN%
   	 
! PER BURST: SPECIFY AR(1) PROCESS WITH A RANDOM AUTOREGRESSION

          phi1 | Y1 ON Y1&1;
          phi2 | Y2 ON Y2&1;
          phi3 | Y3 ON Y3&1;
          
! PER BURST: SPECIFY RANDOM RESIDUAL VARIANCES (WILL BE LOG RESIDUAL VARIANCES AT BETWEEN LEVEL
          rv1 | Y1;
          rv2 | Y2;
          rv3 | Y3;
```
:::

# Step 2: Specify the `BETWEEN` part of the DBMB


::: panel-tabset
## %BETWEEN% Level Model 2a
The syntax for specifying the first part of the `BETWEEN` level of the DMBM excluding random residual variances (i.e., model 2a) is given below.
Here, we define random intercepts per process feature, and subsequently define per burst within-person components for each feature.


``` default
%BETWEEN%

! CREATE RANDOM INTERCEPTS FOR MEANS (Y) and INERTIAS (PHI)

            Y BY Y1-Y3@1;
            phi BY phi1-phi3@1;


! ESTIMATING THE COVARIANCE OF RIs (I.E., THE MACRO-LEVEL COVARIANCES)

            Y WITH phi;


! CREATE WITHIN-PERSON BURST SPECIFIC COMPONENTS FOR Y
! SET THE RESIDUAL VARIANCES TO (ALMOST) ZERO (NO MEASUREMENT ERROR HERE)

            !FOR THE MEANS
            WCY1 BY Y1; Y1@0.001;
            WCY2 BY Y2; Y2@0.001;
            WCY3 BY Y3; Y3@0.001;

            !SAME FOR THE INERTIAS/AUTOREGRESSIONS
            WCPHI1 BY phi1; phi1@0.001;
            WCPHI2 BY phi2; phi2@0.001;
            WCPHI3 BY phi3; phi3@0.001;

! SET COVARIANCE OF WC WITH RIs TO ZERO

            WCY1 WITH Y@0;
            WCY1 WITH phi@0;

            WCPHI1 WITH phi@0;
            WCPHI1 WITH Y@0;

```

## %BETWEEN% Level Model 2b

The syntax for specifying the first part of the `BETWEEN` level of the DMBM including random residual variances (i.e., model 2b) is given below.
Here, we define random intercepts per process feature, and subsequently define per burst within-person components for each feature.

``` default
! CREATE RANDOM INTERCEPTS FOR MEANS (Y), INERTIAS (PHI) AND RESIDUAL VARIANCE (RV)

            Y BY Y1-Y3@1;
            phi BY phi1-phi3@1;
            RV BY rv1-rv3@1;

! ESTIMATING THE COVARIANCES OF RIs (I.E., THE MACRO-LEVEL COVARIANCES)
            Y WITH phi RV;


! CREATE WITHIN-PERSON BURST-SPECIFIC COMPONENTS FOR Y
! SET THE RESIDUAL VARIANCES TO (ALMOST) ZERO (NO MEASUREMENT ERROR HERE)

            !FOR MEANS
            WCY1 BY Y1; Y1@0.001;
            WCY2 BY Y2; Y2@0.001;
            WCY3 BY Y3; Y3@0.001;

            !SAME FOR INERTIAS
            WCPHI1 BY phi1; phi1@0.001;
            WCPHI2 BY phi2; phi2@0.001;
            WCPHI3 BY phi3; phi3@0.001;

            ! SAME FOR RV
            WCRV1 BY rv1; rv1@0.001;
            WCRV2 BY rv2; rv2@0.001;
            WCRV3 BY rv3; rv3@0.001;

! SET COVARIANCE OF WCs WITH RIs TO ZERO
            WCY1 WITH Y@0;
            WCY1 WITH phi@0;
            WCY1 WITH RV@0;

            WCPHI1 WITH phi@0;
            WCPHI1 WITH Y@0;
            WCPHI1 WITH RV@0;

            WCRV1 WITH phi@0;
            WCRV1 WITH Y@0;
            WCRV1 WITH RV@0;
```
:::

# Step 3: Specifying the Meso-level dynamics


::: panel-tabset
## The Meso-level dynamics in Model 2a

The syntax for specifying the meso-level *dyanmics* as part of the `BETWEEN` level of the DMBM excluding random residual variances (i.e., model 2a) is given below.

Here, we use the `PON` (" pairwise on") statement to define autoregressive (*AR*) and cross-lagged (*CL*) effects.
``` default
! SPECIFY THE AUTOREGRESSIONS FOR THE WCs AND CONSTRAIN THESE TO BE INVARIANT OVER TIME

            ! AR EFFECT FOR MEANS
            WCY2 WCY3 PON WCY1 WCY2 (a1);

            ! AR EFFECT FOR INERTIAS
            WCPHI2 WCPHI3 PON WCPHI1 WCPHI2 (a2);


! SPECIFY THE CROSS-LAGGED EFFECTS OF WCs AND CONSTRAIN THESE TO BE INVARIANT OVER TIME

            ! CL EFFECT OF INERTIA ON MEAN
            WCY2 WCY3 PON WCPHI1 WCPHI2 (b1);

            ! CL EFFECT OF MEAN ON INERTIA
            WCPHI2 WCPHI3 PON WCY1 WCY2 (b2);
```

## The Meso-level dynamics in Model 2b

The syntax for specifying the meso-level *dyanmics* as part of the `BETWEEN` level of the DMBM including random residual variances (i.e., model 2b) is given below.

Here, we use the `PON` (" pairwise on") statement to define autoregressive (*AR*) and cross-lagged (*CL*) effects.
``` default
! SPECIFY THE AUTOREGRESSIONS FOR THE WCs AND CONSTRAIN THESE TO BE INVARIANT OVER TIME

            ! AR EFFECT FOR MEANS
            WCY2 WCY3 PON WCY1 WCY2 (a1);

            ! AR EFFECT FOR INERTIAS
            WCPHI2 WCPHI3 PON WCPHI1 WCPHI2 (a2);

            ! AR EFFECT FOR RESIDUAL VARIANCE
            WCRV2 WCRV3 PON WCRV1 WCRV2 (a3);

! SPECIFY THE CROSS-LAGGED EFFECTS OF WCs AND CONSTRAIN THESE TO BE INVARIANT OVER TIME

            ! CL EFFECT OF INERTIAS ON THE MEANS
            WCY2 WCY3 PON WCPHI1 WCPHI2 (b1);
            
            ! CL EFFECT OF RV ON THE MEANS
            WCY2 WCY3 PON WCRV1 WCRV2 (b2);

            ! CL EFFECT OF MEANS ON THE INERTIAS
            WCPHI2 WCPHI3 PON WCY1 WCY2 (b3);
            
            ! CL EFFECT OF RV ON THE INERTIAS
            WCPHI2 WCPHI3 PON WCRV1 WCRV2 (b4);

            ! CL EFFECT OF MEANS ON THE RV
            WCRV2 WCRV3 PON WCY1 WCY2 (b5);
            
            ! CL EFFECT OF INERTIAS ON THE RV
            WCRV2 WCRV3 PON WCPHI1 WCPHI2 (b6);
```
:::

# Step 4: Specifying (residual) covariances of the DMBM

::: panel-tabset
## (Residual) covariances in model 2a

The syntax for specifying the meso-level *(residual) covariances* as part of the `BETWEEN` level of the DMBM excluding random residual variances (i.e., model 2a) is given below.

Here, we use labeling to put contraints on the residual covariances (`(rc1)`) and the residual variances (`(rv1)`). Specifically, we constrain residual covariances to be time invariant, that is, equal at bursts 2 and 3 (e.g., ` WCY2 WITH WCPHI2 (rc1); WCY3 WITH WCPHI3 (rc1);`), a setting which in Mplus v8.11 additionally requires to constrain the residual variances of each feature to be time invariant (e.g., `WCY2 WCY3 (rv1);`).

``` default
! ESTIMATING COVARIANCE OF THE WITHIN-COMPONENTS AT THE FIRST BURST

            WCY1 WITH WCPHI1;
            
! ESTIMATE COVARIANCES OF THE RESIDUALS OF THE WITHIN COMPONENTS AT 2ND AND 3RD BURST
! CONSTRAIN THESE TO BE INVARIANT OVER WAVES

            WCY2 WITH WCPHI2 (rc1);
    	      WCY3 WITH WCPHI3 (rc1);


! CONSTRAIN RESIDUAL VARIANCES OF THE WCs TO BE INVARIANT OVER BURSTS

            WCY2 WCY3 (rv1);
            WCPHI2 WCPHI3 (rv2);
            
! DEFINE OUTPUT

OUTPUT:
            TECH1 TECH8 STDYX FSCOMPARISON;
PLOT:
            TYPE = PLOT3;
            FACTOR =ALL;
```

## (Residual) covariances in model 2b

The syntax for specifying the meso-level *(residual) covariances* as part of the `BETWEEN` level of the DMBM including random residual variances (i.e., model 2b) is given below.

Here, we use labeling to put contraints on the residual covariances (`(rc1)`) and the residual variances (`(rv1)`). Specifically, we constrain residual covariances to be time invariant, that is, equal at bursts 2 and 3 (e.g., ` WCY2 WITH WCPHI2 (rc1); WCY3 WITH WCPHI3 (rc1);`), a setting which in Mplus v8.11 additionally requires to constrain the residual variances of each feature to be time invariant (e.g., `WCY2 WCY3 (rv1);`).


``` default
! ESTIMATE COVARIANCES OF THE WITHIN COMPONENTS AT THE FIRST BURST

            WCY1 WITH WCRV1;
            WCY1 WITH WCPHI1;
            WCPHI1 WITH WCRV1;
            
! ESTIMATE COVARIANCES OF THE RESIDUALS OF THE WITHIN COMPONENTS AT 2ND AND 3RD WA
! CONSTRAIN THESE TO BE INVARIANT OVER WAVES

            WCY2 WITH WCRV2 (rc1);
  	        WCY3 WITH WCRV3 (rc1);

            WCY2 WITH WCPHI2 (rc2);
    	      WCY3 WITH WCPHI3 (rc2);

            WCPHI2 WITH WCRV2 (rc3);
            WCPHI3 WITH WCRV3 (rc3);

! CONSTRAIN RESIDUAL VARIANCES OF THE WCs TO BE INVARIANT OVER WAVES
            WCY2 WCY3 (rv1);
            WCPHI2 WCPHI3 (rv2);
            WCRV2 WCRV3 (rv3);

! DEFINE OUTPUT

OUTPUT:
            TECH1 TECH8 STDYX FSCOMPARISON;
PLOT:
            TYPE = PLOT3;
            FACTOR =ALL;            
```
:::
